plugins {
    id 'com.android.library'
    id "com.google.protobuf"
    id 'kotlin-android'
    id 'maven-publish'

}



android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode VERSION_CODE
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/scripts/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]

            }
        }
    }



    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation "androidx.datastore:datastore:1.0.0-beta01"
    // optional - RxJava2 support
    implementation "androidx.datastore:datastore-rxjava3:1.0.0-beta01"

    implementation "androidx.datastore:datastore-preferences:1.0.0-beta01"
    implementation "androidx.datastore:datastore-preferences-rxjava3:1.0.0-beta01"

    implementation "androidx.room:room-runtime:2.3.0"
    annotationProcessor "androidx.room:room-compiler:2.3.0"


    androidTestImplementation "androidx.room:room-testing:2.3.0"
    implementation  "androidx.datastore:datastore-core:1.0.0-beta01"
    implementation  "com.google.protobuf:protobuf-javalite:3.10.0"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.convention("sources")
    archiveClassifier.set("sources")
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "product".
            product(MavenPublication) {
                // Applies the component for the release build variant.
                artifact sourcesJar

                from components.debug
                ///Users/lincoln.zhu/.m2/repository/com/sumauto/tools
                // You can then customize attributes of the publication as shown below.
                groupId = maven_group_id
                artifactId = 'data'
                version = VERSION_NAME


            }

        }
    }
}